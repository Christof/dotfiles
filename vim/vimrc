set nocompatible

" -----------------------------------------------------------
" Setup Vundle
" -----------------------------------------------------------

filetype off                  " required!

set rtp+=~/.vim/bundle/Vundle.vim
call vundle#begin("~/.vundle")

" let Vundle manage Vundle
" required!
Plugin 'VundleVim/Vundle.vim'

" Plugins
Plugin 'tpope/vim-fugitive'
Plugin 'Shougo/vimproc.vim'
Plugin 'kien/ctrlp.vim'
Plugin 'vim-airline/vim-airline'
Plugin 'vim-airline/vim-airline-themes'
Plugin 'w0rp/ale'
Plugin 'Quramy/tsuquyomi'
" install guide: https://github.com/Valloric/YouCompleteMe
Plugin 'Valloric/YouCompleteMe'
Plugin 'SirVer/ultisnips'
Plugin 'lervag/vimtex'
Plugin 'guns/vim-clojure-static'
Plugin 'tpope/vim-fireplace'
Plugin 'vim-scripts/paredit.vim'
Plugin 'tpope/vim-surround'
Plugin 'tpope/vim-repeat'
Plugin 'tpope/vim-commentary'
Plugin 'tpope/vim-unimpaired'
Plugin 'mileszs/ack.vim'
Plugin 'mattn/emmet-vim'
Plugin 'sjl/gundo.vim'
Plugin 'pangloss/vim-javascript'
Plugin 'leafgarland/typescript-vim'
Plugin 'Shutnik/jshint2.vim'
Plugin 'Raimondi/delimitMate'
Plugin 'marijnh/tern_for_vim'
Plugin 'Lokaltog/vim-easymotion'
Plugin 'tacahiroy/ctrlp-funky'
Plugin 'lfilho/cosco.vim'
Plugin 'editorconfig/editorconfig-vim'
Plugin 'maksimr/vim-jsbeautify'
Plugin 'einars/js-beautify'
Plugin 'tikhomirov/vim-glsl'
Plugin 'christoomey/vim-tmux-navigator'
Plugin 'hauleth/sad.vim'

Plugin 'eagletmt/ghcmod-vim'
Plugin 'eagletmt/neco-ghc'
Plugin 'itchyny/vim-haskell-indent'

" Color themes
Plugin 'https://github.com/vim-scripts/ScrollColors'
Plugin 'altercation/vim-colors-solarized'
Plugin 'jonathanfilip/vim-lucius'
Plugin 'tomasr/molokai'
Plugin 'ratazzi/blackboard.vim'
Plugin 'sjl/badwolf'
Plugin 'Lokaltog/vim-distinguished'
Plugin 'flazz/vim-colorschemes'
Plugin 'lifepillar/vim-solarized8'

call vundle#end()
"
" Additional bundles and before configuration like <leader>
if filereadable(expand("~/.vim/before.vimrc"))
  source ~/.vim/before.vimrc
endif

filetype plugin indent on     " required!
"
" Brief help
" :BundleList          - list configured bundles
" :BundleInstall(!)    - install (update) bundles
" :BundleSearch(!) foo - search (or refresh cache first) for foo
" :BundleClean(!)      - confirm (or auto-approve) removal of unused bundles
"
" see :h vundle for more details or wiki for FAQ
" NOTE: comments after Bundle commands are not allowed.

syntax on

set t_Co=256
set termguicolors
set hidden

" Increase command history capacity
set history=400

set autoread
set autowriteall

" Whitespace
set nowrap                        " don't wrap lines
set tabstop=2                     " a tab is two spaces
set shiftwidth=2                  " an autoindent (with <<) is two spaces
set expandtab                     " use spaces, not tabs
set list                          " Show invisible characters
set listchars=tab:>-,trail:·,extends:>,eol:¬
set backspace=indent,eol,start    " backspace through everything in insert mode

" Search
set hlsearch
set incsearch
set ignorecase
set smartcase

" always show status bar
set laststatus=2

" Always show some context
set scrolloff=3
set sidescrolloff=5

" Disable backup and swap files
set nobackup
set noswapfile

" always show line numbers
set number

" enable per project vimrc files, but disable unsafe commands
set exrc
set secure

" tab navigation
nnoremap <C-tab>   :tabnext<CR>
nnoremap <S-k>     :tabnext<CR>
nnoremap <S-j>     :tabprevious<CR>
nnoremap <C-t>     :tabnew<CR>

" buffer navigation
nnoremap <silent> <Leader>b :bprevious<CR>
nnoremap <silent> <Leader>B :bnext<CR>
nnoremap <silent> <Leader>a :b#<CR> " alternate buffer

" Enable 'scrolling with filtering' using <C-p> and <C-n>
cnoremap <C-p> <Up>
cnoremap <C-n> <Down>

" Join two lines
nnoremap <Leader>j :join<CR>

" repeate last substitution with same flags
nnoremap & :&&<CR>
xnoremap & :&&<CR>

" map ` to <Leader>m because it doesn't work on german keyboards
nnoremap <Leader>m `

" map <C-]> to <Leader>g
nnoremap <Leader>g <C-]>

" open vimrc in new tab
nmap <leader>v :tabedit $MYVIMRC<CR>

" Bind ack to <Leader>f
nnoremap <Leader>f :Ack
nnoremap <Leader>F y:Ack <C-r>=expand('<cword>')<CR>

" map <C-]> to <Leader>g
nnoremap <Leader>g <C-]>

" map jk to esc
inoremap jk <esc>

" Write file as super user
cnoremap wsu w !sudo tee % > /dev/null

" Disable background color erase
" This caused problems in tmux
set t_ut=

" Don't interpret numbers with leading 0 as octal numbers (e.g <C-A> on 007
" yields 008 and not 010
set nrformats=

" use leader r to start renaming of variable under cursor
:nnoremap <Leader>r :%s/\<<C-r><C-w>\>//gc<Left><Left><Left>

" uses paste mode when pasting from the clipboard in insert mode
" and leaves it immeadiately after
inoremap <C-v> <esc>:set paste<cr>a<C-r>+<esc>:set nopaste<cr>a
inoremap <C-b> <esc>:set paste<cr>a<C-r>*<esc>:set nopaste<cr>a

" use leader y and p to copy and paste using the clipboard
nnoremap <leader>y "+y
vnoremap <leader>y "+y
nnoremap <leader>p "+gp
vnoremap <leader>p "+gp
nnoremap <leader>P "+gP
vnoremap <leader>P "+gP

" move in location list
nnoremap en :lnext<CR>
nnoremap eN :lprev<CR>

" Add dash as keyword so that autocompletion of lisp-style works
set iskeyword+=-

" Remap %:h<Tab> to %% for edit, write, saveas and read commands
cnoremap <expr> %% getcmdtype() == ':' ? expand('%:h').'/' : '%%'

" Autosave when focus is lost
au BufLeave,FocusLost * silent! update

"Wrap enables wrapping and rebinds j and k
command! -nargs=* Wrap call s:EnableWrap()
function s:EnableWrap()
  set wrap linebreak nolist
  nnoremap k gk
  nnoremap j gj
endfunction

" Qargs command which populates the args with all files in the quickfix
" list
command! -nargs=0 -bar Qargs execute 'args' QuickfixFilenames()
function! QuickfixFilenames()
  let buffer_numbers = {}
  for quickfix_item in getqflist()
    let buffer_numbers[quickfix_item['bufnr']] = bufname(quickfix_item['bufnr'])
  endfor
  return join(map(values(buffer_numbers), 'fnameescape(v:val)'))
endfunction

" create not existing directories
" code from
" http://stackoverflow.com/questions/4292733/vim-creating-parent-directories-on-save
function! s:MkNonExDir(file, buf)
  if empty(getbufvar(a:buf, '&buftype')) && a:file!~#'\v^\w+\:\/'
    let dir=fnamemodify(a:file, ':h')
    if !isdirectory(dir)
      call mkdir(dir, 'p')
    endif
  endif
endfunction
augroup BWCCreateDir
  autocmd!
augroup END
autocmd BufWritePre * :call s:MkNonExDir(expand('<afile>'), +expand('<abuf>'))

map <leader>k =G<CR>

" format xml
au FileType xml nnoremap <buffer> <leader>k :%!xmllint --format --recover -<cr>

" c++
au FileType cpp nnoremap <buffer> <leader>c :vsp %<.cpp<CR><C-w><C-r>
au FileType cpp nnoremap <buffer> <leader>h :vsp %<.h<CR>

" clang-format
au FileType cpp,cuda,glsl nnoremap <buffer> <Leader>k :%pyf ~/.vim/clang-format.py<CR>
au FileType cpp imap <buffer> <C-K> <ESC>:pyf ~/.vim/clang-format.py<CR>i

" typescript
au FileType typescript nnoremap <buffer> <leader>k :%!tsfmt %< --no-tsconfig --stdin<cr>

" javascript, html, css, scss
au FileType javascript nnoremap <buffer> <leader>k :call JsBeautify()<cr>
au FileType html nnoremap <buffer> <leader>k :call HtmlBeautify()<cr>
au FileType css,scss nnoremap <buffer> <leader>k :call CSSBeautify()<cr>

au FileType cpp set iskeyword-=-

" Delete trailing whitespace
au FileType sh autocmd BufWritePre <buffer> :%s/\s\+$//e

" open .md files as markdown and not as modula2
autocmd BufNewFile,BufReadPost *.md set filetype=markdown
autocmd BufNewFile,BufReadPost *.hglsl set filetype=glsl

" Run tests with mocha
au FileType typescript nnoremap <buffer> <leader>t :execute '!NODE_ENV=test mocha' substitute(expand('%:p'), '\.ts', ".js", '')<CR>
au FileType javascript nnoremap <buffer> <leader>t :execute '!NODE_ENV=test mocha' expand('%:p')<CR>

" Latex
au FileType tex setlocal spell
au FileType tex call s:EnableWrap()

" Haskell
autocmd BufWritePost *.hs GhcModCheckAndLintAsync
" Disable haskell-vim omnifunc
let g:haskellmode_completion_ghc = 0
autocmd FileType haskell setlocal omnifunc=necoghc#omnifunc
au FileType haskell nnoremap <buffer> <leader>k :%!hindent<cr>

" -----------------------------------------------------------
" Plugin configuration
" -----------------------------------------------------------
" vimtex
let g:vimtex_view_use_temp_files=0
let g:vimtex_view_method='zathura'
let g:vimtex_quickfix_ignored_warnings = [
    \ 'Underfull',
    \ 'Overfull',
    \ 'specifier changed to',
    \ 'Package subfig Warning: Your document class has a bad definition',
    \ 'Package hyperref Warning'
  \ ]

" commentary
autocmd FileType cpp,cuda setlocal commentstring=//\ %s

"typescript-vim
let g:typescript_indent_disable = 1

"tsuquyomi
let g:tsuquyomi_disable_default_mappings = 1
nmap <Leader>re :TsuReferences<CR>

" Syntastic
let g:syntastic_cpp_compiler = 'g++'
let g:syntastic_cpp_compiler_options = ' -std=c++11'
let g:syntastic_check_on_open = 1
let g:syntastic_html_tidy_ignore_errors=[" proprietary attribute \"ng-"]
let g:syntastic_always_populate_loc_list = 1
let g:tsuquyomi_disable_quickfix = 1
let g:syntastic_typescript_checkers = ['tsuquyomi']
" Make the list a little shorter
let g:syntastic_loc_list_height = 5

" Ctrlp
let g:ctrlp_user_command = ['.git/', 'cd %s && git ls-files . -co --exclude-standard']
let g:ctrlp_extensions = ['funky']
" Open Ctrlp prepopulated with the current filename without extension
" Can be used to switch between header and cpp files or test files
" Header and cpp files can be switched faster with <leader>h for header
" and <leader>c for cpp if they are in the same directory.
nmap <Leader>fs :let g:ctrlp_default_input = expand('%:t:r') \|
    \ call ctrlp#init(0) \| unlet g:ctrlp_default_input<CR>

let g:ycm_semantic_triggers = {'haskell' : ['.']}

" CtrlPFunky
let g:ctrlp_funky_syntax_highlight = 1
nnoremap <Leader>fu :CtrlPFunky<Cr>
" narrow the list down with a word under cursor
nnoremap <Leader>fU :execute 'CtrlPFunky ' . expand('<cword>')<Cr>

" airline
let g:airline_powerline_fonts=1
let g:airline#extensions#tabline#enabled = 1
set noshowmode

" UltiSnips
let g:UltiSnipsEditSplit = 'vertical'
" handle conflict with YouCompleteMe
function! g:UltiSnips_Complete()
    call UltiSnips#ExpandSnippet()
    if g:ulti_expand_res == 0
        if pumvisible()
            return "\<C-n>"
        else
            call UltiSnips#JumpForwards()
            if g:ulti_jump_forwards_res == 0
               return "\<TAB>"
            endif
        endif
    endif
    return ""
endfunction

au BufEnter * exec "inoremap <silent> " . g:UltiSnipsExpandTrigger . " <C-R>=g:UltiSnips_Complete()<cr>"
let g:UltiSnipsJumpForwardTrigger="<c-j>"
let g:UltiSnipsExpandTrigger = "<c-j>"
let g:UltiSnipsListSnippets="<c-e>"

" YouCompleteMe
let g:ycm_add_preview_to_completeopt = 1
let g:ycm_autoclose_preview_window_after_insertion = 1
let g:ycm_collect_identifiers_from_comments_and_strings = 1
nnoremap <leader>jd :YcmCompleter GoToDefinitionElseDeclaration<CR>
nnoremap <leader>q :YcmCompleter FixIt<CR>
" don't ask before using YCM
let g:ycm_confirm_extra_conf = 0

if !exists('g:ycm_semantic_triggers')
  let g:ycm_semantic_triggers = {}
endif
let g:ycm_semantic_triggers.tex = [
      \ 're!\\[A-Za-z]*cite[A-Za-z]*(\[[^]]*\]){0,2}{[^}]*',
      \ 're!\\[A-Za-z]*ref({[^}]*|range{([^,{}]*(}{)?))',
      \ 're!\\hyperref\[[^]]*',
      \ 're!\\includegraphics\*?(\[[^]]*\]){0,2}{[^}]*',
      \ 're!\\(include(only)?|input){[^}]*',
      \ 're!\\\a*(gls|Gls|GLS)(pl)?\a*(\s*\[[^]]*\]){0,2}\s*\{[^}]*',
      \ 're!\\includepdf(\s*\[[^]]*\])?\s*\{[^}]*',
      \ 're!\\includestandalone(\s*\[[^]]*\])?\s*\{[^}]*',
      \ ]

" Tern for vim
au FileType javascript nnoremap <buffer> <leader>jd :TernDef<CR>

" Ghcmod
map <silent> tw :GhcModTypeInsert<CR>
map <silent> ts :GhcModSplitFunCase<CR>
map <silent> tq :GhcModType<CR>
map <silent> te :GhcModTypeClear<CR>

" Necoghc
let g:necoghc_enable_detailed_browse = 1

" Unimpaired
nmap ü [
nmap + ]
omap ü [
omap + ]
xmap ü [
xmap + ]

" SAD
nmap e <Plug>(sad-change-forward)
xmap e <Plug>(sad-change-forward)
nmap E <Plug>(sad-change-backward)
xmap E <Plug>(sad-change-backward)

" fugitive
nnoremap <leader>gs :Gstatus<CR>

" gundo
nnoremap <leader>u :GundoToggle<CR>

" cosco.vim
autocmd FileType javascript,css noremap <buffer> <silent> <leader>, :call cosco#commaOrSemiColon()<CR>
autocmd FileType javascript,css inoremap <buffer> <silent> <leader>, <c-o>:call cosco#commaOrSemiColon()<CR>

" elm.vim
let g:elm_detailed_complete = 1
let g:elm_syntastic_show_warnings = 1

" Customizations like colortheme or key bindings
if filereadable(expand("~/.vim/after.vimrc"))
  source ~/.vim/after.vimrc
endif

" Source this file when it's saved
if has("autocmd")
  autocmd! bufwritepost vimrc source $MYVIMRC
endif
